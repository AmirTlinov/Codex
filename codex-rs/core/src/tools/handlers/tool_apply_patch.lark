start: begin_patch hunk+ end_patch

begin_patch: "*** Begin Patch" LF
end_patch: "*** End Patch" LF

hunk: add_hunk
    | delete_hunk
    | update_hunk
    | insert_before_symbol
    | insert_after_symbol
    | replace_symbol_body

add_hunk: "*** Add File: " filename LF add_line+
delete_hunk: "*** Delete File: " filename LF
update_hunk: "*** Update File: " filename LF move_to? change+

insert_before_symbol: "*** Insert Before Symbol: " symbol_target LF add_line+
insert_after_symbol: "*** Insert After Symbol: " symbol_target LF add_line+
replace_symbol_body: "*** Replace Symbol Body: " symbol_target LF add_line+

filename: /(.+)/
symbol_target: /(.+)/
add_line: "+" /(.*)/ LF -> line

move_to: "*** Move to: " filename LF
change: change_context? change_line+ eof_line?
change_context: ("@@" | "@@ " /(.+)/) LF
change_line: ("+" | "-" | " ") /(.*)/ LF
eof_line: "*** End of File" LF

%import common.LF
